buildscript {
	ext {
		springBootVersion = '1.5.7.RELEASE'
		springDependencyManagementVersion = '1.0.3.RELEASE'
	}
	repositories {
        maven {
            credentials { 
                username System.env.ArtifactoryUser
                password System.env.ArtifactoryKey
            }
            url System.env.REPO_MAVEN_RELEASE
        }
        maven {
            credentials { 
                username System.env.ArtifactoryUser
                password System.env.ArtifactoryKey
            }
            url System.env.REPO_GRADLE_PLUGINS
        }
    }
    dependencies {
		classpath("gradle.plugin.com.gorylenko.gradle-git-properties:gradle-git-properties:1.4.17")
		classpath("io.spring.gradle:dependency-management-plugin:${springDependencyManagementVersion}")
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
	}
}

gitProperties {
	dateFormat = "yyyy-MM-dd'T'HH:mmZ"
    dateFormatTimeZone = "UTC"
    gitRepositoryRoot = new File("${project.rootDir}/../../../..")
}

import org.apache.tools.ant.filters.*

processResources {
    filter ReplaceTokens, tokens: [
		"application.name": project.property("application.name"),
		"application.description": project.property("application.description"),
        "application.version": project.property("version")
    ]
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'maven-publish'
apply plugin: 'org.springframework.boot'
apply plugin: "io.spring.dependency-management"
apply plugin: "com.gorylenko.gradle-git-properties"

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:Dalston.SR4"
        mavenBom "io.pivotal.spring.cloud:spring-cloud-services-dependencies:1.5.0.RELEASE"
    }
}

dependencies {
	compileOnly('org.projectlombok:lombok:1.16.18')
	compile('org.springframework.boot:spring-boot-starter-actuator')
	compile('org.springframework.boot:spring-boot-starter-data-jpa')
	compile('org.springframework.boot:spring-boot-starter-data-rest')
	compile('org.springframework.boot:spring-boot-starter-hateoas')
	compile('org.springframework.data:spring-data-rest-hal-browser')
	compile('org.springframework.boot:spring-boot-starter-web')
	compile('io.pivotal.spring.cloud:spring-cloud-services-starter-config-client')
	compile('io.pivotal.spring.cloud:spring-cloud-services-starter-service-registry')
	runtime('com.h2database:h2')
	testCompile('org.springframework.boot:spring-boot-starter-test')
}

repositories {
	maven {
        credentials { 
            username System.env.ArtifactoryUser
            password System.env.ArtifactoryKey
        }
        url System.env.REPO_MAVEN_RELEASE
    }
}

jar {
    excludes = ['**/application.yml']
}

task execJar (type: Jar, dependsOn: jar) {
    classifier = 'exec'
    from sourceSets.main.output
}

bootRepackage  {
    withJarTask = tasks['execJar']
}

publishing {
    publications {
        maven(MavenPublication) {
			groupId 'io.pivotal'
            from components.java
        }
    }
	repositories {
        mavenLocal()
    }
}